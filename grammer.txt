program        → declaration* EOF ;

declaration    → varDecl
               | statement ;
varDecl        → "var" variable ( "," variable )* ";" ;
variable       → IDENTIFIER ( "=" expression)? ;

statement      → exprStmt
               | ifStmt
               | printStmt
               | block ;

ifStmt         → "if" "(" expression ")" statement
               ( "else" statement )? ;

block          → "{" declaration "}" ;
exprStmt       → expression ";" ;
printStmt      → "print" expression ";" ;


expression     → assignment ;
assignment     → IDENTIFIER "=" assignment | logic_or ;

logic_or       → logic_and ( ( "or" | "||" ) logic_and )* ;
logic_and      → bitwise_or ( ( "and" | "&&" ) bitwise_or )* ;
bitwise_or     → bitwise_xor ( "|" bitwise_xor )* ;
bitwise_xor    → bitwise_and ( "^" bitwise_and )* ;
bitwise_and    → equality ( "&" equality )* ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → shift ( ( ">" | ">=" | "<" | "<=" ) shift )* ;
shift          → term ( ( ">>" | "<<" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → power ( ( "/" | "*" ) power )* ;
power          → unary ( "**" unary )* ;
unary          → ( "!" | "-" | "~" ) unary
               | primary ;
primary        → NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")" | IDENTIFIER ;
